/**
 * File:	modules/SMTData.ycp
 * Package:	Configuration of SMT
 * Summary:	SMT global data and functions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id:$
 */

{
    textdomain "smt";

    module "SMTData";

    import "Message";
    import "Service";
    import "Report";
    import "FileUtils";
    import "String";
    import "Label";
    import "Popup";
    import "PackageSystem";
    import "Wizard";
    import "Progress";
    import "Directory";
    import "RunlevelEd";
    import "ProductControl";

    map <string, map <string, string> > all_credentials = $[];

    string smt_conf = "/etc/smt.conf";

    // script entered into a crontab
    string cron_mirror_script = "/usr/bin/smt-mirror.pl";
    // cron 
    string path_to_cron = "/etc/smt.d/novell.com-smt";

    string path_to_sync_script = "/usr/sbin/smt-ncc-sync";
    string user_for_sync_script = "root";

    string server_cert = "/etc/ssl/certs/YaST-CA.pem";
    string apache_cert = "/srv/www/htdocs/smt.crt";

    // own control file
    string smt_control_file = "/usr/share/YaST2/control/smt_control.xml";

    list <map> smt_cron_settings = [];

    boolean first_run = nil;

    string first_run_file = sformat ("%1/smt-already-adjusted", Directory::vardir);

    boolean smt_enabled = nil;

    list <string> smt_services = ["smt"];

    string smt_database = "mysql";

    string NCCcredentials_file = "/etc/zypp/credentials.d/NCCcredentials";

    global string GetNCCcredentialsFile () {
	return NCCcredentials_file;
    }

    global boolean SystemIsRegistered () {
	return (FileUtils::Exists (NCCcredentials_file) == true);
    }

    global string GetCredentials (string location, string entry_name);

    string _smt_user = nil;
    boolean _smt_user_loaded = false;

    /**
     * Returns SMT user (LOCAL->smtUser) or nil if not set or erroneous
     */
    string GetSMTUser () {
	if (_smt_user_loaded == true) {
	    return _smt_user;
	}

	_smt_user_loaded = true;

	_smt_user = SMTData::GetCredentials ("LOCAL", "smtUser");
	if (_smt_user == nil || _smt_user == "") {
	    // The default user
	    _smt_user = "smt";
	}


	integer user_exists = (integer) SCR::Execute (.target.bash,
	    sformat ("/usr/bin/getent passwd '%1'", String::Quote (_smt_user))
	);

	if (user_exists == 0) {
	    y2milestone ("User %1 exists", _smt_user);
	} else {
	    y2error ("User %1 doesn't exist!", _smt_user);
	    // Pop-up error message, %1 is replaced with the SMT user name,
	    // %2 with the SMT config file
	    Report::Error (sformat (_("SMT configuration is broken.
LOCAL->smtUser %1 defined in %2 does not exist on the system."), _smt_user, smt_conf));
	}

	return _smt_user;
    }

    /**
     * Returns whether a NCCcredentials file is accessible by SMT user
     * defined in LOCAL->smtUser. Returns nil if the file does not exist.
     *
     * @return boolean whether accessible
     */
    boolean CredentialsFileAccessible () {
	string smt_user = GetSMTUser();
	if (smt_user == nil || smt_user == "") return nil;

	return ((integer) SCR::Execute (.target.bash, sformat (
	    "echo '' | /usr/bin/sudo -S -u '%1' /bin/cat '%2'",
	    String::Quote (smt_user),
	    String::Quote (NCCcredentials_file)
	)) == 0);
    }

    /**
     * Adjusts NCCcredentials file permissions to be accessible
     * by SMT user defined in LOCAL->smtUser.
     * By default it adjusts file ACL to "read", or it changes the
     * file owner and permissions to smt:root.
     */
    void AdjustNCCCredentialsFileAccess () {
	string smt_user = GetSMTUser();

	if (smt_user == nil || smt_user == "") {
	    y2error ("LOCAL->smtUser: %1", smt_user);
	    return;
	}

	string cmd = sformat ("/usr/bin/setfacl -m u:%1:r '%2'", smt_user, String::Quote (NCCcredentials_file));
	boolean setfacl = ((integer) SCR::Execute (.target.bash, cmd) == 0);
	y2milestone ("Adjusting file ACL (%1) returned %2", cmd, setfacl);

	// check whether the file is really accessible
	if (CredentialsFileAccessible()) {
	    if (FileUtils::Exists ("/usr/bin/getfacl")) {
		y2milestone ("Credentials file is accessible now: %1", SCR::Execute (.target.bash_output,
		    sformat ("/usr/bin/getfacl --access '%1' 2>/dev/null", String::Quote (NCCcredentials_file))));
	    } else {
		y2milestone ("Credentials file is accessible now");
	    }
	    return;
	}

	cmd = sformat ("/bin/chown %1:root '%2'; /bin/chmod ug+r '%2'", smt_user, String::Quote (NCCcredentials_file));
	boolean chownchmod = ((integer) SCR::Execute (.target.bash, cmd) == 0);
	y2milestone ("Adjusting file access (%1) returned %2", cmd, chownchmod);
    }

    /**
     * Checks (and adjusts) the NCCcredentials file permissions the be readable
     * by SMT user defined in LOCAL->smtUser. Returns if accessible and/or permissions
     * successfuly set. If the file does not exist, nil is returned.
     *
     * @return boolean if succesful (file exists and is accessible)
     */
    global boolean CheckAndAdjustCredentialsFileAccess () {
	if (SystemIsRegistered() != true) {
	    y2warning ("Credentials file does not exist");
	    return nil;
	}

	boolean state = CredentialsFileAccessible();
	y2milestone ("Initial state: NCCcredentials are readable by SMT user: %1", state);

	if (state == true) {
	    return true;
	} else if (state == nil) {
	    return false;
	}

	AdjustNCCCredentialsFileAccess();

	state = CredentialsFileAccessible();
	y2milestone ("Final state: NCCcredentials are readable by SMT user: %1", state);

	return state;
    }

    /**
     * Returns list of scheduled NU mirrorings.
     *
     * @return list <map>
     *
     * @struct [
     *   $[
     *     "command":"...",
     *     "day_of_month":"...",
     *     "day_of_week":"...",
     *     "hour":"...",
     *     "minute":"...",
     *     "month":"...",
     *     "user":"...",
     *   ],
     *   ...
     * ]
     */
    global list <map> GetCronSettings () {
	return smt_cron_settings;
    }

    /**
     * Adds new cron job.
     *
     * @param map new_cron_job
     *
     * @see GetCronSettings()
     */
    global void AddNewCronJob (map new_cron_job) {
	new_cron_job["user"] = user_for_sync_script;

	smt_cron_settings = add (smt_cron_settings, new_cron_job);
    }

    /**
     * Replaces the current cron job settings with new ones.
     *
     * @param integer cron_job_id (offset)
     * @param map new_settings
     *
     * @see GetCronSettings()
     */
    global void ReplaceCronJob (integer cron_job_id, map new_settings) {
	if (cron_job_id == nil || cron_job_id < 0) {
	    y2error ("Undefined offset: %1", cron_job_id);
	    return;
	}

	new_settings["user"] = user_for_sync_script;

	smt_cron_settings[cron_job_id] = new_settings;
    }

    /**
     * Removes a current cron job.
     *
     * @param integer cron_job_id (offset)
     *
     * @see GetCronSettings()
     */
    global void RemoveCronJob (integer cron_job_id) {
	if (cron_job_id == nil || cron_job_id < 0) {
	    y2error ("Undefined offset: %1", cron_job_id);
	    return;
	}

	smt_cron_settings = remove (smt_cron_settings, cron_job_id);
    }

    /**
     * Reads whether the configuration process has been already done.
     */
    global void ReadFirstRun () {
	if (! FileUtils::Exists (first_run_file)) {
	    y2milestone ("File %1 doesn't exist -> this is a first-run", first_run_file);
	    first_run = true;
	} else {
	    y2milestone ("Not a first-time run...");
	    first_run = false;
	}
    }

    /**
     * Writes that the configuration has been already done.
     */
    global void WriteFirstRunStatus () {
	if (! (boolean) SCR::Write (.target.ycp, String::Quote (first_run_file), [])) {
	    y2error ("Cannot create file %1", first_run_file);
	}
    }

    /**
     * Returns whether the current YaST run is the first run of the SMT configuration.
     */
    global boolean IsFirstRun () {
	return first_run;
    }

    boolean smt_enabled_initial = nil;

    global void ReadSMTServiceStatus () {
	smt_enabled = false;
	smt_enabled_initial = false;

	if (IsFirstRun() == nil) {
	    y2error ("Cannot evaluate first_run!");
	} else if (IsFirstRun() == true) {
	    smt_enabled_initial = true;
	    smt_enabled = true;
	    return;
	}

	// default (changed later to false if something doesn't work)
	smt_enabled = true;
	smt_enabled_initial = true;

	foreach (string one_service, smt_services, {
	    if (! Service::Enabled (one_service)) {
		smt_enabled = false;
		smt_enabled_initial = false;
	    }
	});
    }

    global boolean GetSMTServiceStatus () {
	return smt_enabled;
    }

    global void SetSMTServiceStatus (boolean new_status) {
	if (new_status == nil) {
	    y2error ("Cannot set 'nil' status!");
	    return;
	}

	smt_enabled = new_status;
    }

    global boolean WriteSMTServiceStatus () {
	if (GetSMTServiceStatus() == true) {

	    foreach (string one_service, smt_services, {
		list <string> dep_s = RunlevelEd::ServiceDependencies (one_service, true);
		if (dep_s != nil) {
		    y2milestone ("These services need to be enabled: %1", dep_s);
		    foreach (string one_dep, dep_s, {
			if (! Service::Enabled (one_dep) && ! Service::Enable (one_dep)) {
			    y2error ("Unable to enable service: %1", one_dep);
			    return;
			}
		    });
		}

		Service::Enable (one_service);
		Service::Start (one_service);
	    });
	} else {
	    foreach (string one_service, smt_services, {
		Service::Stop (one_service);
		Service::Disable (one_service);
	    });
	}

	return true;
    }

    /**
     * Returns value for credentials entry.
     *
     * @see SetCredentials
     *
     * @param string entry_name
     * @return string value
     */
    global string GetCredentials (string location, string entry_name) {
	if (! haskey (all_credentials, location)) {
	    y2warning ("Key %1 not defined", location);
	    return "";
	} else if (! haskey (all_credentials[location]:$[], entry_name)) {
	    y2warning ("Key %1/%2 not defined", location, entry_name);
	    return "";
	}

	return all_credentials[location, entry_name]:"";
    }

    /**
     * Sets name/value credential pairs.
     *
     * @see GetCredentials
     *
     * @param string entry_name
     * @param string value
     */
    global void SetCredentials (string location, string entry_name, string value) {
	if (! haskey (all_credentials, location)) {
	    all_credentials[location] = $[];
	}

	all_credentials[location, entry_name] = value;
    }

    /**
     * Reads the current SMT credentials into the memory
     *
     * @return boolean if successful
     */
    global boolean ReadCredentials () {
	all_credentials = $[];

	all_credentials["NU"] = $[];

	foreach (string section, (list <string>) SCR::Dir (.smt_conf.section), {
	    path section_path = add (.smt_conf.value, section);

	    foreach (string one_entry, (list <string>) SCR::Dir (section_path), {
		SetCredentials (section, one_entry, (string) SCR::Read (add (section_path, one_entry)));
	    });
	});

	return true;
    }

    /**
     * Writes the current credentials to the SMT configuration file.
     *
     * @return boolean if successful
     */
    global boolean WriteCredentials () {
	boolean failed = false;

	foreach (string base_key, map <string, string> current_credentials, all_credentials, {
	    foreach (string key, string value, current_credentials, {
		if (! (boolean) SCR::Write (add (add (.smt_conf.value, base_key), key), value)) {
		    y2error ("Cannot write to smt.conf/%1/%2", base_key, key);
		    failed = true;
		}
	    });
	});

	if (failed == false) {
	    if (! (boolean) SCR::Write (.smt_conf, nil)) {
		y2error ("Cannot write to smt.conf");
		failed = true;
	    }
	}

	return (failed == false);
    }

    /**
     * Function checks and adjusts the apache configuration
     * to be usable by SMT.
     *
     * @return boolean if successful
     */
    global boolean CheckAndAdjustApacheConfiguration () {
	string apache_conf_file = "/etc/sysconfig/apache2";

	boolean apache_conf_changed = false;

	if (! (boolean) SCR::RegisterAgent (.http_server_conf, `ag_ini (`SysConfigFile (apache_conf_file)))) {
	    y2error ("Cannot register agent");
	    Message::CannotWriteSettingsTo (apache_conf_file);
	    return false;
	}

	// "perl" in /etc/sysconfig/apache2:APACHE_MODULES
	string ap_modules = (string) SCR::Read (.http_server_conf.APACHE_MODULES);
	string ap_modules_old = ap_modules;

	list <string> ap_modules_l = splitstring (ap_modules, " \t");
	ap_modules_l = filter (string one_ap_module, ap_modules_l, {
	    return (one_ap_module != nil && one_ap_module != "");
	});
	ap_modules_l = toset ((list <string>) union (ap_modules_l, ["perl"]));
	ap_modules = mergestring (ap_modules_l, " ");

	if (ap_modules != ap_modules_old) {
	    y2milestone ("Writing APACHE_MODULES");
	    SCR::Write (.http_server_conf.APACHE_MODULES, ap_modules);
	    apache_conf_changed = true;
	}

	// SSL in /etc/sysconfig/apache2:APACHE_SERVER_FLAGS
	string ap_serflag = (string) SCR::Read (.http_server_conf.APACHE_SERVER_FLAGS);
	string ap_serflag_old = ap_serflag;

	list <string> ap_serflag_l = splitstring (ap_serflag, " \t");
	ap_serflag_l = filter (string one_ap_serflag, ap_serflag_l, {
	    return (one_ap_serflag != nil && one_ap_serflag != "");
	});
	ap_serflag_l = toset ((list <string>) union (ap_serflag_l, ["SSL"]));
	ap_serflag = mergestring (ap_serflag_l, " ");

	if (ap_serflag != ap_serflag_old) {
	    y2milestone ("Writing APACHE_SERVER_FLAGS");
	    SCR::Write (.http_server_conf.APACHE_SERVER_FLAGS, ap_serflag);
	    apache_conf_changed = true;
	}

	// Something has been changed
	// Apache conf needs writing
	// and Apache service needs restarting
	if (apache_conf_changed) {
	    y2milestone ("Writing sysconfig/apache conf");
	    if (! (boolean) SCR::Write (.http_server_conf, nil)) {
		y2error ("Cannot write apache2 conf");
		Message::CannotWriteSettingsTo (apache_conf_file);
		return false;
	    }
	}

	if (! (boolean) SCR::UnregisterAgent (.http_server_conf)) {
	    y2error ("Cannot unregister agent");
	}

	return true;
    }

    global boolean RunSmallSync () {
	if (! FileUtils::Exists (path_to_sync_script)) {
	    y2error ("Sync script doesn't exist");
	    Report::Error (sformat (_("Synchronization script %1 does not exist.

Please, check your SMT installation."), path_to_sync_script));
	    return false;
	}

	y2milestone ("Running sync: %1", path_to_sync_script);

	map cmd_out = (map) SCR::Execute (.target.bash_output, path_to_sync_script);
	if (cmd_out["exit"]:-1 != 0) {
	    UI::OpenDialog (
		`HBox (
		    `VSpacing (19),
		    `VBox (
			`HSpacing (70),
			`Left (`Label (_("Running the synchronization script failed."))),
			`RichText (
			    `opt (`hstretch, `vstretch),
			    sformat (_("<p><b><big>Details:</big></b></p>
<p><b>stdout:</b><br><pre>%1</pre></p>
<p><b>stderr:</b><br><pre>%2</pre></p>"), cmd_out["stdout"]:"", cmd_out["stderr"]:"")
			),
			`PushButton (`id (`ok), `opt (`default, `key_F10), Label::OKButton())
		    )
		)
	    );
	    UI::UserInput();
	    UI::CloseDialog();
	}

	return true;
    }

    /**
     * Reads SMT cron settings and fills up internal data variables.
     */
    global void ReadCronSettings () {
	if (! FileUtils::Exists (path_to_cron)) {
	    y2milestone ("Creating file %1", path_to_cron);
	    map cmd = (map) SCR::Execute (.target.bash_output, sformat ("touch '%1'", String::Quote (path_to_cron)));

	    if (cmd["exit"]:-1 != 0) {
		y2error ("Cannot create file %1: %2", path_to_cron, cmd);
	    }
	}

	smt_cron_settings = (list <map>) SCR::Read (.smt_cron);
    }

    global void CronRandomize () {
	if (! IsFirstRun()) {
	    return;
	}

	y2milestone ("First-run: randomizing cron times");

	// to get different 'random' numbers :->>>
	srandom();

	integer hour_smt_daily = 1;
	integer minute_smt_daily = 0;

	// randomize smt-daily
	integer counter = -1;
	foreach (map one_cron_job, GetCronSettings(), {
	    counter = counter + 1;

	    if (regexpmatch (one_cron_job["command"]:"", "smt-daily")) {
		// 00 - 02
		hour_smt_daily = random (2);
		minute_smt_daily = random (59);

		one_cron_job["hour"] = tostring (hour_smt_daily);
		one_cron_job["minute"] = tostring (minute_smt_daily);
		y2milestone ("smt-daily randomized, hour: %1, minute: %2", one_cron_job["hour"]:"", one_cron_job["minute"]:"");
		ReplaceCronJob (counter, one_cron_job);
	    }
	});

	// randomize smt-gen-report
	counter = -1;
	foreach (map one_cron_job, GetCronSettings(), {
	    counter = counter + 1;

	    if (regexpmatch (one_cron_job["command"]:"", "smt-gen-report")) {
		// +4 hours to smt-daily
		one_cron_job["hour"] = tostring (hour_smt_daily + 4);
		one_cron_job["minute"] = tostring (minute_smt_daily);
		y2milestone ("smt-gen-report randomized, hour: %1, minute: %2", one_cron_job["hour"]:"", one_cron_job["minute"]:"");
		ReplaceCronJob (counter, one_cron_job);
	    }
	});

	// randomize smt-repeated-register
	// 450 (seconds) == 7.5 minutes
	SetCredentials ("LOCAL", "rndRegister", tostring (random (450)));
    }

    global boolean WriteCronSettings () {
	string service_name = "cron";

	if (smt_cron_settings == nil || size (smt_cron_settings) == 0) {
	    if (FileUtils::Exists (path_to_cron) && ! (boolean) SCR::Execute (.target.remove, path_to_cron)) {
		y2error ("Cannot remove %1 cron-file", path_to_cron);
	    } else {
		y2milestone ("No cron settings at all");
		return true;
	    }
	}

	if (! (boolean) SCR::Write (.smt_cron, smt_cron_settings)) {
	    y2error ("Writing cron failed");
	    Report::Error (_("Cannot write cron settings."));
	    return false;
	}

	if (size (smt_cron_settings) > 0) {
	    if (! Service::Enabled (service_name)) {
		Service::Enable (service_name);
	    } else {
		y2milestone ("Service cron already enabled, skipping ...");
	    }
	}

	if (! Service::Restart (service_name)) {
	    y2error ("Reloading %1 failed", service_name);
	    // TRANSLATORS: error message, %1 is replaced with a service-name
	    Report::Error (sformat (_("Reloading service %1 has failed."), service_name));
	    return false;
	}

	return true;
    }

    boolean ca_already_called = false;

    /**
     * Handles a missing CA certificate.
     * Returns boolean value whether CA checking should be finished.
     *
     * @return boolean whether to finish the CA checking
     */
    boolean HandleMissingCACert () {
	y2milestone ("CA file %1 doesn't exist", server_cert);
	string ca_mgm_package = "yast2-ca-management";

	if (Popup::AnyQuestion (
	    // TRANSLATORS: Pop-up question headline
	    _("Missing Server Certificate"),
	    // TRANSLATORS: Pop-up question, %1 is replaced with a file name
	    sformat (_("Server certificate %1 does not exist.
Would you like to run CA management to create one?

The server certificate is vitally important for the NU server to support SSL.
"),
		server_cert),
		_("&Run CA management"),
		_("&Skip"),
		(ca_already_called ? `focus_no : `focus_yes))
	) {
	    // Package needs to be installed
	    if (PackageSystem::CheckAndInstallPackagesInteractive ([ca_mgm_package])) {
		y2milestone ("Running ca_mgm");
		ca_already_called = true;

		Wizard::OpenNextBackDialog();
		boolean progress_orig = Progress::set (false);

		// bnc #400782
		// Custom control file can make ProductControl not to find anything
		string previous_custom_control_file = ProductControl::custom_control_file;
		if (previous_custom_control_file != "") {
		    y2milestone ("Previous custom_control_file: %1", previous_custom_control_file);
		}

		// bnc #471162
		// Use own control file (do not depend on the system one which might be missing)
		ProductControl::custom_control_file = smt_control_file;
		ProductControl::Init();

		// Call installation proposal (service: CA)
		any ret = WFM::call ("inst_proposal", [$["proposal":"smt_ca"]]);

		y2milestone ("Restoring previous custom_control_file: %1", previous_custom_control_file);
		ProductControl::custom_control_file = previous_custom_control_file;
		ProductControl::Init();

		y2milestone ("Service proposal returned: %1", ret);
		Progress::set (progress_orig);
		Wizard::CloseDialog();

		return false;
	    // Package is not installed and couldn't be installed
	    } else {
		Report::Error (sformat (
		    _("Cannot run CA management because package %1 is not installed."),
		    ca_mgm_package
		));
		    return true;
	    }
	} else {
	    y2warning ("User doesn't want to run CA management.");
	    return true;
	}
    }

    /**
     * Function checks whether a server certificate exists and copies it
     * to the apache directory. If a server certificate doesn't exist,
     * user is offered to run the CA management.
     *
     * @return boolean if successful
     */
    global boolean WriteCASettings () {
	boolean ret = false;

	// check the existence of file and offer to run CA managament
	// if it doesn't then check the existence again... and again...
	while (true) {
	    // Server certificate is missing
	    if (! FileUtils::Exists (server_cert)) {
		if (HandleMissingCACert()) {
		    break;
		}

	    // Server certificate exists
	    } else {
		ret = true;
		break;
	    }
	}

	return ret;
    }

    string AskForRootPassword (string message) {
	string ret = nil;

	UI::OpenDialog (`VBox (
	    `VSpacing (1),
	    `Heading (_("Database root Password Required")),
	    `VSpacing (1),
	    `HBox (
		`HSpacing (2),
		`VBox (
		    (message != "" && message != nil ?
			`VBox (`Label (message), `VSpacing (1))
			:
			`Empty()
		    ),
		    `Password (`id (`root_password), _("Enter the MySQL root &Password"))
		),
		`HSpacing (2)
	    ),
	    `VSpacing (1),
	    `HBox (
		`PushButton (`id (`ok), `opt (`default, `key_F10), Label::OKButton()),
		`HSpacing (2),
		`PushButton (`id (`cancel), `opt (`key_F9), Label::CancelButton())
	    ),
	    `VSpacing (1)
	));

	UI::SetFocus (`id (`root_password));

	any user_ret = UI::UserInput();
	if (user_ret == `ok) {
	    ret = (string) UI::QueryWidget (`id (`root_password), `Value);
	}

	UI::CloseDialog();

	return ret;
    }

    string mysql_root_password = "";

    // bnc #389804
    boolean database_already_exists = false;

    /**
     * @return boolean whether to try again
     */
    boolean HandleSMTDBSetupRetcode (integer retcode_was) {
	// 2 : Cannot read the SMT configuration file
	if (retcode_was == 2) {
	    return Report::AnyQuestion (
		sformat (_("Unable to read %1"), "/etc/smt.conf"),
		_("SMT was not able to read the configuration file.
Please, check the file and click Retry.

To skip this, click Skip.
"),
		_("&Retry"),
		_("&Skip"),
		`yes_button
	    );

	// 4 : Cannot connect to database (wrong mysql root password?)
	} else if (retcode_was == 4) {
	    mysql_root_password = AskForRootPassword (_("SMT was not able to connect to the database.
The root password was probably wrong.
"));
	    if (mysql_root_password != nil)
		return true;
	    else
		return false;

	// 5 : SMT database already exists
	} else if (retcode_was == 5) {
	    database_already_exists = true;
	    y2milestone ("SMT database already exists");
	    return false;

	// 7 : Cannot create Database smt
	} else if (retcode_was == 7) {
	    return Report::AnyQuestion (
		_("Cannot create database"),
		_("SMT was not able to create the database.
Click Retry to try again.
"),
		_("&Retry"),
		_("&Skip"),
		`yes_button
	    );

	// 21: Database migration failed
	} else if (retcode_was == 21) {
	    return Report::AnyQuestion (
		_("Database migration failed"),
		_("SMT has failed to migrate the database.
Click Retry to try again.
"),
		_("&Retry"),
		_("&Skip"),
		`yes_button
	    );

	} else {
	    y2warning ("Return code %1 not handled", retcode_was);
	}

	return false;
    }

    string AskForNewRootPassword () {
	string ret = nil;

	UI::OpenDialog (`VBox (
	    `VSpacing (1),
	    `Heading (_("Adjusting New Database root Password")),
	    `VSpacing (1),
	    `HBox (
		`HSpacing (2),
		`VBox (
		    `Label (_("The current MySQL root password is empty.

For security reasons, please, set a new one.")),
		    `VSpacing (1),
		    `Password (`id (`new_root_password_1), _("New MySQL root &Password")),
		    `Password (`id (`new_root_password_2), _("New Password &Again"))
		),
		`HSpacing (2)
	    ),
	    `VSpacing (1),
	    `HBox (
		`PushButton (`id (`ok), `opt (`default, `key_F10), Label::OKButton()),
		`HSpacing (2),
		`PushButton (`id (`cancel), `opt (`key_F9), Label::CancelButton())
	    ),
	    `VSpacing (1)
	));

	UI::SetFocus (`id (`new_root_password_1));

	while (true) {
	    any user_ret = UI::UserInput();

	    if (user_ret == `cancel) {
		ret = nil;
		break;
	    } else if (user_ret == `ok) {
		string pass_1 = (string) UI::QueryWidget (`id (`new_root_password_1), `Value);
		string pass_2 = (string) UI::QueryWidget (`id (`new_root_password_2), `Value);

		if (pass_1 == nil || pass_1 == "") {
		    UI::SetFocus (`id (`new_root_password_1));
		    Report::Error (_("Set up a new password, please."));
		    continue;
		} else if (pass_1 != pass_2) {
		    UI::SetFocus (`id (`new_root_password_2));
		    Report::Error (_("The first and the second password do not match."));
		    continue;
		}

		y2milestone ("New password provided");
		ret = pass_1;
		break;
	    }
	}

	UI::CloseDialog();

	return ret;
    }

    string GetMysqlHostname () {
	string hostname = "localhost";

	string db_config = GetCredentials ("DB","config");
	foreach (string one_db_item, splitstring (db_config, ";"), {
	    if (regexpmatch (one_db_item, ".*host=.*")) {
		hostname = regexpsub (one_db_item, ".*host=(.*)", "\\1");
		y2milestone ("Hostname found: %1", hostname);
		break;
	    }
	});

	return hostname;
    }

    boolean SetNewRootPassword (string new_mysql_root_password) {
	y2milestone ("Adjusting new root password");

	string mysql_command = sformat (
	    "SET PASSWORD FOR root@%1=PASSWORD('%2');",
	    GetMysqlHostname(),
	    String::Quote (new_mysql_root_password)
	);

	string mysql_command_file = sformat ("%1/smt-rootpass-filename", Directory::tmpdir);
	SCR::Write (.target.string, mysql_command_file, "");
	// rw only for owner
	SCR::Execute (.target.bash, sformat ("chmod 0600 '%1'", String::Quote (mysql_command_file)));
	SCR::Write (.target.string, mysql_command_file, mysql_command);

	string cmd_cmd = sformat ("mysql -u root -h %1 < '%2'", GetMysqlHostname(), mysql_command_file);
	map cmd = (map) SCR::Execute (.target.bash_output, cmd_cmd);
	if (cmd["exit"]:-1 != 0) {
	    y2error ("Cannot set new root password: %1", cmd);
	    Report::Error (_("Setting up new MySQL root password failed."));
	    return false;
	}

	y2milestone ("Successful %1", cmd);
	return true;
    }

    global boolean StartDatabaseIfNeeded () {
	if (! Service::Start (smt_database)) {
	    y2error ("Cannot start database");
	    return false;
	}

	return true;
    }

    global boolean WriteDatabaseSettings () {
	// Keeping the service stopped
	if (smt_enabled_initial == false && ! GetSMTServiceStatus()) {
	    y2milestone ("SMT is disabled, not adjusting database...");
	    return true;
	}

	// check whether mysql root password is empty
	boolean root_password_empty = ((integer) SCR::Execute (.target.bash,
	    sformat ("echo 'show databases;' | mysql -u root -h %1 2>/dev/null", GetMysqlHostname())
	) == 0);

	// root password is empty
	// ask user to set a new one
	if (root_password_empty) {
	    // exception: SMT user is root
	    if (GetSMTUser() == "root") {
		y2warning ("SMT user is root, using root's new password");
		mysql_root_password = GetCredentials ("DB","pass");
	    } else {
		y2warning ("mysql root password is empty");
		mysql_root_password = AskForNewRootPassword();

		if (mysql_root_password == nil) {
		    y2error ("New password not provided");
		    return false;
		}
	    }

	    if (! SetNewRootPassword (mysql_root_password)) {
		y2warning ("Unable to set new root password, using empty one");
		mysql_root_password = "";
	    }
	// root password has been already set
	} else {
	    y2milestone ("mysql root password is not empty, asking for it...");
	    mysql_root_password = AskForRootPassword (_("SMT needs to set up the database."));
	    if (mysql_root_password == nil) {
		y2error ("Mysql-root password not provided");
		return false;
	    }
	}

	string smt_command_file = sformat ("%1/smt-command-filename", Directory::tmpdir);
	SCR::Write (.target.string, smt_command_file, "");
	// rw only for owner
	SCR::Execute (.target.bash, sformat ("chmod 0600 '%1'", String::Quote (smt_command_file)));

	boolean ret = false;

	while (true) {
	    // /usr/lib/SMT/bin/smt-db setup --yast
	    // * mysql root password
	    // * smt user name
	    // * smt user password
	    // * verify smt user password
	    string smt_db_command = sformat (
		"%1\n%2\n%3\n%4\n",
		mysql_root_password,
		GetCredentials ("DB","user"),
		GetCredentials ("DB","pass"),
		GetCredentials ("DB","pass")
	    );

	    // always write it again, bnc 387414
	    SCR::Write (.target.string, smt_command_file, smt_db_command);

	    y2milestone ("Calling smt-db setup");
	    integer retcode = (integer) SCR::Execute (.target.bash, sformat ("/usr/lib/SMT/bin/smt-db setup --yast < '%1'", smt_command_file));
	    y2milestone ("smt-db setup returned: %1", retcode);

	    if (retcode == 0) {
		y2milestone ("Success");
		ret = true;
		break;
	    } else {
		if (! HandleSMTDBSetupRetcode (retcode)) {
		    y2milestone ("Finishing SMT-DB setup");
		    break;
		}
	    }
	}

	SCR::Execute (.target.remove, smt_command_file);

	return ret;
    }

    string initial_password = nil;

    /**
     * Function remembers initial SMT-user password
     */
    global void StorePasswordTMP () {
	if (initial_password == nil) {
	    initial_password = GetCredentials ("DB","pass");
	} else {
	    y2error ("Initial password already set!");
	}
    }

    /**
     * Function changes the SMT-user passowrd if different
     * than the initial one
     */
    global boolean ChangePasswordIfDifferent () {
	// Keeping the service stopped
	if (smt_enabled_initial == false && ! GetSMTServiceStatus()) {
	    y2milestone ("SMT is disabled, not adjusting database...");
	    return true;
	}

	// bnc #389804
	// exit 5 from smt-db setup means that database exists and cpw
	// should be called
	if (database_already_exists != true) {
	    y2milestone ("Database and user were just created, skipping cpw ...");
	    return true;
	}

	string new_password = GetCredentials ("DB","pass");

	if (initial_password == new_password) {
	    y2milestone ("SMT-user password is the same");
	    return true;
	}

	// /usr/lib/SMT/bin/smt-db cpw
	// * mysql root password
	// * smt user name
	// * smt user password
	// * verify smt user password
	string smt_cpw_command = sformat (
	    "%1\n%2\n%3\n",
	    initial_password,
	    GetCredentials ("DB","pass"),
	    GetCredentials ("DB","pass")
	);

	string smt_command_file = sformat ("%1/smt-cpw-command-filename", Directory::tmpdir);
	SCR::Write (.target.string, smt_command_file, "");
	// rw only for owner
	SCR::Execute (.target.bash, sformat ("chmod 0600 '%1'", String::Quote (smt_command_file)));
	SCR::Write (.target.string, smt_command_file, smt_cpw_command);

	y2milestone ("Calling smt-db cpw");
	integer retcode = (integer) SCR::Execute (.target.bash, sformat ("/usr/lib/SMT/bin/smt-db cpw --yast < '%1'", smt_command_file));
	y2milestone ("smt-db setup returned: %1", retcode);

	if (retcode != 0) {
	    Report::Error (_("Cannot change SMT user password."));
	    return false;
	} else {
	    return true;
	}

	SCR::Execute (.target.remove, smt_command_file);
    }
}
