/**
 * File:	modules/SMTData.ycp
 * Package:	Configuration of SMT
 * Summary:	SMT global data and functions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id:$
 */

{
    textdomain "smt";

    module "SMTData";

    import "Message";
    import "Service";
    import "Report";
    import "FileUtils";
    import "String";
    import "Label";
    import "Popup";
    import "PackageSystem";
    import "Wizard";
    import "Progress";

    map <string, string> NU_credentials = $[];

    // script entered into a crontab
    string cron_mirror_script = "/usr/bin/smt-mirror.pl";
    // cron 
    string path_to_cron = "/etc/cron.d/yast2-smt";

    string path_to_sync_script = "/usr/sbin/smt-ncc-sync";
    string user_for_sync_script = "root";

    string server_cert = "/etc/ssl/certs/YaST-CA.pem";
    string apache_cert = "/srv/www/htdocs/smt.crt";

    list <map> smt_cron_settings = [];

    /**
     * Returns list of scheduled NU mirrorings.
     *
     * @return list <map>
     *
     * @struct [
     *   $[
     *     "command":"...",
     *     "day_of_month":"...",
     *     "day_of_week":"...",
     *     "hour":"...",
     *     "minute":"...",
     *     "month":"...",
     *     "user":"...",
     *   ],
     *   ...
     * ]
     */
    global list <map> GetCronSettings () {
	return smt_cron_settings;
    }

    /**
     * Adds new cron job.
     *
     * @param map new_cron_job
     *
     * @see GetCronSettings()
     */
    global void AddNewCronJob (map new_cron_job) {
	new_cron_job["user"] = user_for_sync_script;
	new_cron_job["command"] = path_to_sync_script;

	smt_cron_settings = add (smt_cron_settings, new_cron_job);
    }

    /**
     * Replaces the current cron job settings with new ones.
     *
     * @param integer cron_job_id (offset)
     * @param map new_settings
     *
     * @see GetCronSettings()
     */
    global void ReplaceCronJob (integer cron_job_id, map new_settings) {
	if (cron_job_id == nil || cron_job_id < 0) {
	    y2error ("Undefined offset: %1", cron_job_id);
	    return;
	}

	new_settings["user"] = user_for_sync_script;
	new_settings["command"] = path_to_sync_script;

	smt_cron_settings[cron_job_id] = new_settings;
    }

    /**
     * Removes a current cron job.
     *
     * @param integer cron_job_id (offset)
     *
     * @see GetCronSettings()
     */
    global void RemoveCronJob (integer cron_job_id) {
	if (cron_job_id == nil || cron_job_id < 0) {
	    y2error ("Undefined offset: %1", cron_job_id);
	    return;
	}

	smt_cron_settings = remove (smt_cron_settings, cron_job_id);
    }

    /**
     * Reads the current SMT credentials into the memory
     *
     * @return boolean if successful
     */
    global boolean ReadCredentials () {
	foreach (string one_entry, ["NUUser", "NUPass", "NUUrl"], {
	    NU_credentials[one_entry] = (string) SCR::Read (add (.smt_conf.value.NU, one_entry));
	});

	return true;
    }

    /**
     * Writes the current credentials to the SMT configuration file.
     *
     * @return boolean if successful
     */
    global boolean WriteCredentials () {
	boolean failed = false;

	foreach (string one_entry, ["NUUser", "NUPass", "NUUrl"], {
	    if (! (boolean) SCR::Write (add (.smt_conf.value.NU, one_entry), NU_credentials[one_entry]:"")) {
		y2error ("Cannot write to smt.conf:%1", one_entry);
		failed = true;
	    }
	});

	if (failed == false) {
	    if (! (boolean) SCR::Write (.smt_conf, nil)) {
		y2error ("Cannot write to smt.conf");
		failed = true;
	    }
	}

	return (failed == false);
    }

    /**
     * Returns value for credentials entry.
     *
     * @see SetCredentials
     *
     * @param string entry_name
     * @return string value
     */
    global string GetCredentials (string entry_name) {
	if (! haskey (NU_credentials, entry_name)) {
	    y2warning ("Key %1 not defined", entry_name);
	    return "";
	}

	return NU_credentials[entry_name]:"";
    }

    /**
     * Sets name/value credential pairs.
     *
     * @see GetCredentials
     *
     * @param string entry_name
     * @param string value
     */
    global void SetCredentials (string entry_name, string value) {
	NU_credentials[entry_name] = value;
    }

    /**
     * Function checks and adjusts the apache configuration
     * to be usable by SMT.
     *
     * @return boolean if successful
     */
    global boolean CheckAndAdjustApacheConfiguration () {
	string apache_conf_file = "/etc/sysconfig/apache2";

	boolean apache_conf_changed = false;

	if (! (boolean) SCR::RegisterAgent (.http_server_conf, `ag_ini (`SysConfigFile (apache_conf_file)))) {
	    y2error ("Cannot register agent");
	    Message::CannotWriteSettingsTo (apache_conf_file);
	    return false;
	}

	// "perl" in /etc/sysconfig/apache2:APACHE_MODULES
	string ap_modules = (string) SCR::Read (.http_server_conf.APACHE_MODULES);
	string ap_modules_old = ap_modules;

	list <string> ap_modules_l = splitstring (ap_modules, " \t");
	ap_modules_l = filter (string one_ap_module, ap_modules_l, {
	    return (one_ap_module != nil && one_ap_module != "");
	});
	ap_modules_l = toset ((list <string>) union (ap_modules_l, ["perl"]));
	ap_modules = mergestring (ap_modules_l, " ");

	if (ap_modules != ap_modules_old) {
	    y2milestone ("Writing APACHE_MODULES");
	    SCR::Write (.http_server_conf.APACHE_MODULES, ap_modules);
	    apache_conf_changed = true;
	}

	// SSL in /etc/sysconfig/apache2:APACHE_SERVER_FLAGS
	string ap_serflag = (string) SCR::Read (.http_server_conf.APACHE_SERVER_FLAGS);
	string ap_serflag_old = ap_serflag;

	list <string> ap_serflag_l = splitstring (ap_serflag, " \t");
	ap_serflag_l = filter (string one_ap_serflag, ap_serflag_l, {
	    return (one_ap_serflag != nil && one_ap_serflag != "");
	});
	ap_serflag_l = toset ((list <string>) union (ap_serflag_l, ["SSL"]));
	ap_serflag = mergestring (ap_serflag_l, " ");

	if (ap_serflag != ap_serflag_old) {
	    y2milestone ("Writing APACHE_SERVER_FLAGS");
	    SCR::Write (.http_server_conf.APACHE_SERVER_FLAGS, ap_serflag);
	    apache_conf_changed = true;
	}

	// Symlink conf

	// Something has been changed
	// Apache conf needs writing
	// and Apache service needs restarting
	if (apache_conf_changed) {
	    y2milestone ("Writing sysconfig/apache conf");
	    if (! (boolean) SCR::Write (.http_server_conf, nil)) {
		y2error ("Cannot write apache2 conf");
		Message::CannotWriteSettingsTo (apache_conf_file);
		return false;
	    }

	    if (! Service::Reload ("apache2")) {
		y2error ("Reloading apache2 failed");
		// TRANSLATORS: error message, %1 is replaced with a service-name
		Report::Error (sformat (_("Reloading service has %1 failed."), "apache2"));
		return false;
	    }
	}

	if (! (boolean) SCR::UnregisterAgent (.http_server_conf)) {
	    y2error ("Cannot unregister agent");
	}

	// Enabling the Apache service
	if (! Service::Enabled ("apache2")) {
	    Service::Enable ("apache2");
	} else {
	    y2milestone ("Service already enabled, skipping ...");
	}

	return true;
    }

    global boolean RunSmallSync () {
	if (! FileUtils::Exists (path_to_sync_script)) {
	    y2error ("Sync script doesn't exist");
	    Report::Error (sformat (_("Synchronization script %1 does not exist.

Please, check your SMT installation."), path_to_sync_script));
	    return false;
	}

	map cmd_out = (map) SCR::Execute (.target.bash_output, path_to_sync_script);
	if (cmd_out["exit"]:-1 != 0) {
	    UI::OpenDialog (
		`HBox (
		    `VSpacing (19),
		    `VBox (
			`HSpacing (70),
			`Left (`Label (_("Running the synchronization script failed."))),
			`RichText (
			    `opt (`hstretch, `vstretch),
			    sformat (_("<p><b>Details:</b></p>
<p><b>stdout:</b> %1</p>
<p><b>stderr:</b> %2</p>"), cmd_out["stdout"]:"", cmd_out["stderr"]:"")
			),
			`PushButton (`id (`ok), `opt(`default), Label::OKButton())
		    )
		)
	    );
	    UI::UserInput();
	    UI::CloseDialog();
	}

	return true;
    }

    global void ReadCronSettings () {
	if (! FileUtils::Exists (path_to_cron)) {
	    y2milestone ("Creating file %1", path_to_cron);
	    map cmd = (map) SCR::Execute (.target.bash_output, sformat ("touch '%1'", String::Quote (path_to_cron)));

	    if (cmd["exit"]:-1 != 0) {
		y2error ("Cannot create file %1: %2", path_to_cron, cmd);
	    }
	}

	smt_cron_settings = (list <map>) SCR::Read (.smt_cron);
    }

    global boolean WriteCronSettings () {
	string service_name = "cron";

	if (smt_cron_settings == nil || size (smt_cron_settings) == 0) {
	    if (FileUtils::Exists (path_to_cron) && ! (boolean) SCR::Execute (.target.remove, path_to_cron)) {
		y2error ("Cannot remove %1 cron-file", path_to_cron);
	    } else {
		y2milestone ("No cron settings at all");
		return true;
	    }
	}


	if (! (boolean) SCR::Write (.smt_cron, smt_cron_settings)) {
	    y2error ("Writing cron failed");
	    Report::Error (_("Cannot write cron settings."));
	    return false;
	}

	if (size (smt_cron_settings) > 0) {
	    if (! Service::Enabled (service_name)) {
		Service::Enable (service_name);
	    } else {
		y2milestone ("Service cron already enabled, skipping ...");
	    }
	}

	if (! Service::Restart (service_name)) {
	    y2error ("Reloading %1 failed", service_name);
	    // TRANSLATORS: error message, %1 is replaced with a service-name
	    Report::Error (sformat (_("Reloading service has %1 failed."), service_name));
	    return false;
	}

	return true;
    }

    boolean ca_already_called = false;

    /**
     * Handles a missing CA certificate.
     * Returns boolean value whether CA checking should be finished.
     *
     * @return boolean whether to finish the CA checking
     */
    boolean HandleMissingCACert () {
	y2milestone ("CA file %1 doesn't exist", server_cert);
	string ca_mgm_package = "yast2-ca-management";

	if (Popup::AnyQuestion (
	    // TRANSLATORS: Pop-up question headline
	    _("Missing Server Certificate"),
	    // TRANSLATORS: Pop-up question, %1 is replaced with a file name
	    sformat (_("Server certificate %1 doesn't exist.
Would you like to run CA management to create one?

Server certificate is vitally important for NU server to support SSL."),
		server_cert),
		_("&Run CA management"),
		_("&Skip"),
		(ca_already_called ? `focus_no : `focus_yes))
	) {
	    // Package needs to be installed
	    if (PackageSystem::CheckAndInstallPackagesInteractive ([ca_mgm_package])) {
		y2milestone ("Running ca_mgm");
		ca_already_called = true;

		Wizard::OpenNextBackDialog();
		boolean progress_orig = Progress::set (false);
		any ret = WFM::call ("inst_proposal", [$["proposal":"service"]]);
		y2milestone ("Service proposal returned: %1", ret);
		Progress::set (progress_orig);
		Wizard::CloseDialog();

		return false;
	    // Package is not installed and couldn't be installed
	    } else {
		Report::Error (sformat (
		    _("Cannot run CA management bacause package %1 is not installed."),
		    ca_mgm_package
		));
		    return true;
	    }
	} else {
	    y2warning ("User doesn't want to run CA management.");
	    return true;
	}
    }

    /**
     * Function checks whether a server certificate exists and copies it
     * to the apache directory. If a server certificate doesn't exist,
     * user is offered to run the CA management.
     *
     * @return boolean if successful
     */
    global boolean WriteCASettings () {
	boolean ret = false;

	// check the existence of file and offer to run CA managament
	// if it doesn't then check the existence again... and again...
	while (true) {
	    // Server certificate is missing
	    if (! FileUtils::Exists (server_cert)) {
		if (HandleMissingCACert()) {
		    break;
		}

	    // Server certificate exists
	    } else {
		// Copy the certificate to an apache configuration
		string cmd = sformat ("/bin/cp --force '%1' '%2'", String::Quote (server_cert), String::Quote (apache_cert));
		map cmd_out = (map) SCR::Execute (.target.bash_output, cmd);

		if (cmd_out["exit"]:-1 != 0) {
		    y2error ("Cannot copy server certificate >%1<: %2", cmd, cmd_out);
		    Report::Error (sformat (_("Copying server certificate %1 to %2 failed.
Server will not support HTTPS"), server_cert, apache_cert));
		} else {
		    ret = true;
		}

		break;
	    }
	}

	return ret;
    }
}
