/**
 * File:	include/yep/dialogs.ycp
 * Package:	Configuration of yep
 * Summary:	Dialogs definitions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{
    textdomain "yep";

    include "yep/helps.ycp";
    include "yep/complex.ycp";

    import "Wizard";
    import "Popup";
    import "YEPData";
    import "Label";
    import "Confirm";
    import "Progress";
    import "Message";

    integer sl = 800;

    term CredentialsDialogContent () {
	return `HBox (
		`HStretch(),
		`HSquash (`VBox (
		    `VStretch(),
		    `Left (`HSquash (`MinWidth (
			45,
			`TextEntry (`id ("NUUrl"), _("NU UR&L"))
		    ))),
		    `Left (`HSquash (`MinWidth (
			20,
			`TextEntry (`id ("NUUser"), _("NU &User"))
		    ))),
		    `Left (`HSquash (`MinWidth (
			20,
			`Password (`id ("NUPass"), _("NU &Password"))
		    ))),
		    `VSpacing (1),
		    `PushButton (`id ("test_NU_credentials"), `opt(`key_F6), _("&Test...")),
		    `VStretch()
		)),
		`HStretch()
	    );
    }

    term ScheduledDownloadsDialogContent () {
	return `VBox (
	    `VStretch(),
	    `HBox (
		`HStretch(),
		`Label (_("FIXME: ... ")),
		`HStretch()
	    ),
	    `VStretch()
	);
    }

    symbol ReadDialog() {
	// Checking for root's permissions
	if (!Confirm::MustBeRoot())
	    return `abort;

	Progress::New (
	    // TRANSLATORS: Dialog caption
	    _("Initializing YEP Configuration"),
	    " ",
	    1,
	    [
		// TRANSLATORS: Progress step
		_("Read YEP configuration"),
	    ],
	    [
		// TRANSLATORS: Progress step
		_("Reading YEP configuration..."),
		Message::Finished(),
	    ],
	    ""
	);
	Wizard::SetTitleIcon("yast");
	Wizard::RestoreHelp(HELPS["read"]:"");

	Progress::NextStage();
	sleep (sl);

	YEPData::ReadCredentials();

	Progress::NextStage();
	sleep (sl);

	Progress::Finish();

	return `next;
    }

    symbol WriteDialog() {
	Progress::New (
	    // TRANSLATORS: Dialog caption
	    _("Writing YEP Configuration"),
	    " ",
	    2,
	    [
		// TRANSLATORS: Progress step
		_("Adjust YEP configuration"),
		// TRANSLATORS: Progress step
		_("Adjust Web-server configuration"),
	    ],
	    [
		// TRANSLATORS: Progress step
		_("Adjusting YEP configuration..."),
		// TRANSLATORS: Progress step
		_("Adjust Web-server configuration"),
		Message::Finished(),
	    ],
	    ""
	);
	Wizard::SetTitleIcon("yast");
	Wizard::RestoreHelp(HELPS["write"]:"");

	Progress::NextStage();
	sleep (sl);

	YEPData::WriteCredentials();

	Progress::NextStage();
	sleep (sl);

	YEPData::CheckAndAdjustApacheConfiguration();

	Progress::NextStage();
	sleep (sl);

	Progress::Finish();

	return `next;
    }

    void InitCredentialsDialog (string id) {
	foreach (string one_entry, ["NUUrl", "NUUser", "NUPass"], {
	    string value = YEPData::GetCredentials (one_entry);
	    if (value == nil) value = "";
	    UI::ChangeWidget (`id (one_entry), `Value, value);
	});
    }

    void StoreCredentialsDialog (string id, map event) {
	foreach (string one_entry, ["NUUrl", "NUUser", "NUPass"], {
	    YEPData::SetCredentials (one_entry, (string) UI::QueryWidget (`id (one_entry), `Value));
	});
    }

    boolean TestCredentials () {
	UI::OpenDialog (`MinSize (52, 12, `VBox (
	    // TRANSLATORS: LogView label
	    `LogView (`id ("test_log"), _("&Test Details"), 5, 100),
	    `VSpacing (1),
	    `PushButton (`id (`ok), `opt (`default), Label::OKButton())
	)));

	// complex.ycp
	boolean ret = CredentialsTest ("test_log");

	if (ret == true) {
	    // TRANSLATORS: LogView line
	    UI::ChangeWidget (`id ("test_log"), `LastLine, "\n" + _("Test result: success") + "\n");
	} else {
	    // TRANSLATORS: LogView line
	    UI::ChangeWidget (`id ("test_log"), `LastLine, "\n" + _("Test result: failed") + "\n");
	}

	UI::UserInput();
	UI::CloseDialog();

	return ret;
    }

    symbol HandleCredentialsDialog (string id, map event) {
	any action = event["ID"]:nil;

	if (action == "test_NU_credentials") {
	    StoreCredentialsDialog (id, event);
	    TestCredentials();
	}

	return nil;
    }

    void InitScheduledDownloadsDialog (string id) {
	return nil;
    }

    symbol HandleScheduledDownloadsDialog (string id, map event) {
	any action = event["ID"]:nil;

	return nil;
    }

    void StoreScheduledDownloadsDialog (string id, map event) {
	return nil;
    }

    boolean ReallyExit () {
	// TRANSLATORS: yes-no popup
	return Popup::YesNo (_("Really exit?
All changes will be lost."));
    }
}
